/*
# _____     ___ ____     ___ ____
#  ____|   |    ____|   |        | |____|
# |     ___|   |____ ___|    ____| |    \    PS2DEV Open Source Project.
#-----------------------------------------------------------------------
# Copyright 2001-2004, ps2dev - http://www.ps2dev.org
# Licenced under Academic Free License version 2.0
# Review ps2sdk README & LICENSE files for further details.
#
# $Id: linkfile 798 2005-02-07 06:21:37Z herben $
# Linkfile script for ee-ld
*/
INCLUDE ../sections.ld

ENTRY(_start);

SECTIONS {
	_SECTION_EXCEPTION_HANDLER = EXCEPTION_HANDLER;
	_SECTION_UNPATCH = UNPATCH;
	_SECTION_MODULE_DEFINITIONS = MODULE_DEFINITIONS;
	_SECTION_PATCH_POINTERS = PATCH_POINTERS;
	_SECTION_PATCH_HASH = PATCH_HASH;
	_SECTION_USB_MODULES = USB_MODULES;
	_SECTION_PATCH = PATCH;
	_SECTION_CUSTOM_GAME_MODE = CUSTOM_GAME_MODE;

	.text PATCH : {
		_SECTION_PATCH_START = . ;
		*(.text)
		KEEP(*(.config))
		*(.text.*)
		*(.gnu.linkonce.t*)
		QUAD(0)
	}


	PROVIDE(_etext = .);
	PROVIDE(etext = .);

	.reginfo : { *(.reginfo) }

	/* Static data.  */
	.rodata ALIGN(128): {
		*(.rodata)
		*(.rodata.*)
		*(.gnu.linkonce.r*)
	}

	.data ALIGN(128): {
		_fdata = . ;
		*(.data)
		*(.data.*)
		*(.gnu.linkonce.d*)
		SORT(CONSTRUCTORS)
	}

	.sdata ALIGN(128): {
		*(.sdata)
		*(.sdata.*)
		*(.gnu.linkonce.s*)
		*(.scommon)
		*(.scommon.*)
	}

	_SECTION_PATCH_END = . ;
	PROVIDE(SECTION_PATCH_END = .);
}
